buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'org.inferred.processors' version '1.2.10'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'cloud.cosmin.checklister'
version = '0.0.2'
sourceCompatibility = 10

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/it/java')
        }
        resources.srcDir file('src/it/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    doFirst {
        dockerCompose.exposeAsEnvironment(integrationTest)
    }
}

dependencies {
    // web
    compile('org.springframework.boot:spring-boot-starter-web')

    // database
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.hibernate:hibernate-java8')
    runtime('org.postgresql:postgresql:42.2.2')
    compile('org.flywaydb:flyway-core:5.0.7')

    compile('org.bitbucket.cowwoc:requirements-core:4.0.4-RC')

    // because of Java 9+
    // https://stackoverflow.com/questions/43574426/how-to-resolve-java-lang-noclassdeffounderror-javax-xml-bind-jaxbexception-in-j#43574427
    // https://blog.sourced-bvba.be/article/2017/12/17/java9-spring/
    compile('javax.xml.bind:jaxb-api:2.3.0')
    compile('com.sun.xml.bind:jaxb-impl:2.3.0')
    compile('org.glassfish.jaxb:jaxb-runtime:2.3.0')
    compile('javax.activation:activation:1.1.1')

    // dev tools
    compile("org.springframework.boot:spring-boot-devtools")

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.boot:spring-boot-test-autoconfigure')

//    integrationTestCompile('org.seleniumhq.selenium:selenium-java:3.13.0')
//    integrationTestCompile('org.seleniumhq.selenium:selenium-remote-driver:3.13.0')
}

bootJar {
    archiveName = "app.jar"
}
